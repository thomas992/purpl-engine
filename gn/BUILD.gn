#  Default toolchain flags
#
#  Copyright 2021 MobSlicer152
#  This file is part of Purpl Engine
#
#  Licensed under the Apache License, Version 2.0 (the "License");
#  you may not use this file except in compliance with the License.
#  You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
#  Unless required by applicable law or agreed to in writing, software
#  distributed under the License is distributed on an "AS IS" BASIS,
#  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#  See the License for the specific language governing permissions and
#  limitations under the License.

config("compiler_defaults") {
  # Vcpkg headers and libraries
  include_dirs = [ "//vcpkg_installed/$current_cpu-$current_os/include" ]
  lib_dirs = [
    "//vcpkg_installed/$target_cpu-$target_os/debug/lib",
    "//vcpkg_installed/$target_cpu-$target_os/lib",
  ]

  asmflags = []
  cflags = []
  ldflags = []

  if (is_win) {
    cflags += [
      "/std:c++20",
      "/Zo",
    ]
  } else {
    cflags += [
      "-fPIC",
      "--std=c++20",
      "-Wall",
      "-Wextra",
    ]
  }

  if (is_debug) {
    if (is_win) {
      asmflags += [
        "/Zd",
        "/Zi",
      ]
      cflags += [ "/Zi" ]
      ldflags += [ "/debug" ]
    } else {
      cflags += [ "-g" ]
    }
  } else {
    if (is_win) {
      cflags += [
        "/O2",
        "/Oi-",
      ]
    } else {
      cflags += [ "-O2" ]
    }
  }
}
